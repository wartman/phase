namespace Phase::Language

use Std::Io::Path

class TypePath {
  static of(str: String) {
    var parts = str.split('::').filter { |it:String| it != null && it.length > 0 }
    var name = parts.pop()
    // todo: params
    return TypePath(
      ns: parts,
      name: name
    )
  }

  new(
    this.ns: Array<String>,
    this.name: String,
    this.params: ?Array<TypePath> = null,
    this.isAbsolute: Bool = false,
    this.isNullable: Bool = false
  ) {
    if (this.params == null) this.params = []
  }

  toString() {
    var path = this.ns.concat([ this.name ]).join('::')
    if (this.isAbsolute) {
      path = '::${path}' 
    }
    if (this.isNullable) {
      path = '?${path}'
    }
    if (this.params != null && this.params.length > 0) {
      return '${path}<${ this.params.map { it.toString() }.join(', ') }>'
    }
    return path
  }

  toFileName(): String {
    return Path
      .join(this.ns)
      .with(this.name)
      .normalized()
      .toString()
  }

  notAbsolute(): TypePath {
    var tp = this |> clone()
    tp.isAbsolute = false
    return tp
  }
}
